apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

// Create the pom configuration:
def pomConfig = {
    developers {
        developer {
            id developerId
            name developerName
            email developerEmail
        }
    }

    scm {
        url developerUrl
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from "$buildDir/docs/javadoc"
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                artifact sourcesJar
                artifact javadocJar

                groupId publishedGroupId
                artifactId artifact
                version libraryVersion

                // Define this explicitly if using implementation or api configurations
                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', libraryDescription)
                    root.appendNode('name', libraryName)
                    root.children().last() + pomConfig
                }
            }
        }
    }
}

Properties properties = new Properties()
if (project.rootProject.file('local.properties').canRead()) {
    properties.load(project.rootProject.file("local.properties").newDataInputStream())
    ext.bintray_user = properties.getProperty('bintray.user')
    ext.bintray_key = properties.getProperty('bintray.apikey')
} else {
    ext.bintray_user = System.getenv('bintray.user')
    ext.bintray_key = System.getenv('bintray.apikey')
}

bintray {
    user = bintray_user
    key = bintray_key
    publications = ['release']
    override = true
    pkg {
        repo = bintrayRepo
        name = bintrayName
        licenses = allLicenses
        publish = true
        vcsUrl = gitUrl
        publicDownloadNumbers = true
        version {
            name = libraryVersion
            desc = libraryDescription
            released = new Date()
            vcsTag = libraryVersion
        }
    }
}